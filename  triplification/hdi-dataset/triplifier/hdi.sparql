PREFIX ex: <http://example.org/nswi144/hdi/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX qb: <http://purl.org/linked-data/cube#>

CONSTRUCT {
    ?regionIRI a skos:Concept, ex:Region ;
        skos:notation ?region .

    ?countryIRI a ex:Country ;
        ex:countryName ?country ;
        ex:countryCode ?iso3 ;
        ex:locatedIn ?regionIRI .

    ?measurementIRI a qb:Observation ;
        qb:dataSet ex:hdiDataset ;
        ex:year ?yearTyped ;
        ex:hdiValue ?hdiValueTyped ;
        ex:refCountry ?countryIRI ;
        ex:hdiRank ?hdiRankTyped .
}
WHERE {
    BIND(IRI(CONCAT("http://example.org/nswi144/hdi/region_", ?region)) AS ?regionIRI)
    BIND(IRI(CONCAT("http://example.org/nswi144/hdi/country_", ?iso3)) AS ?countryIRI)
    BIND(IRI(CONCAT("http://example.org/nswi144/hdi/hdiMeasurement_", ?iso3, "_", ?year)) AS ?measurementIRI)
    
    BIND(STRDT(?year, xsd:gYear) AS ?yearTyped)
    BIND(STRDT(?hdiValue, xsd:decimal) AS ?hdiValueTyped)
    
    # Optionally bind hdiRank only if the year is "2022"
    OPTIONAL {
        FILTER(?year = "2022")
        BIND(STRDT(?hdi_rank_2022, xsd:integer) AS ?hdiRankTyped)
    }
    
    # Filter out rows with empty HDI values or invalid year formats
    FILTER(?hdiValue != "" && REGEX(?year, "^[0-9]{4}$"))
}
